/*
 * Copyright 2019 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'java-library'
    id 'org.owasp.dependencycheck' version '11.1.1'
    id 'org.springframework.boot' version "${springBootVersion}"
    id "com.epam.drill.integration.cicd" version "0.1.6"
    id "org.openapi.generator" version "7.11.0"
}

import org.owasp.dependencycheck.reporting.ReportGenerator

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/build-commons.gradle"
apply from: "$scriptsUrl/build-info.gradle"
apply from: "$scriptsUrl/release-service.gradle"
apply from: "$scriptsUrl/signing.gradle"
apply from: "$scriptsUrl/copy-database-scripts.gradle"

project.hasProperty('sealightsSession') && sealightsSession?.trim() ? apply(from: 'sealights.gradle') : println('No sealights session')

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
//    if (!releaseMode) {
    maven { url 'https://jitpack.io' }
//    }
}

java {
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext['spring-boot.version'] = "${springBootVersion}"

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + '5.14.3' : 'com.epam.reportportal:commons-bom:5.14.3')
        mavenBom('io.zonky.test.postgres:embedded-postgres-binaries-bom:16.2.0')
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
}

println("Release mode: $releaseMode")
dependencies {
    if (releaseMode) {
        println("Using release dependencies")
        implementation 'com.github.reportportal:commons:fdfdcdf'
        implementation 'com.github.reportportal:commons-dao:4cedb72f'
        implementation('com.github.reportportal:plugin-api:8f089e4') {
            exclude group: 'com.github.reportportal:commons-dao', module: 'commons-dao'
        }
    } else {
        println("Using snapshot dependencies")
        implementation 'com.github.reportportal:commons:fdfdcdf'
        implementation 'com.github.reportportal:commons-dao:80d1492'
        implementation ('com.github.reportportal:plugin-api:8f089e4') {
            exclude group: 'com.github.reportportal:commons-dao', module: 'commons-dao'
        }
    }
    implementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.security:spring-security-acl'
    implementation 'org.springframework.security:spring-security-core'
    implementation 'org.springframework.security:spring-security-oauth2-core'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-web'
    implementation 'org.springframework:spring-context-support'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'

    //Fix CVE-2025-24813
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.41'
    implementation 'org.apache.tomcat.embed:tomcat-embed-el:10.1.41'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:10.1.41'


    implementation 'com.opencsv:opencsv:5.10'
    implementation 'commons-beanutils:commons-beanutils:1.11.0'
    implementation "org.jooq:jooq:${jooqVersion}"

    // Optional for spring-boot-starter-amqp
    implementation "com.rabbitmq:http-client:5.3.0"
    implementation("org.apache.httpcomponents.client5:httpclient5:5.4.3") // supports gzip encoding

    // check authentication error response format for versions higher than 6.21.3
    implementation('net.sf.jasperreports:jasperreports:6.21.3') { //TODO: consider upgrade to 7.0.1+
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-xml'
    }
    // JasperReport's export to XLS uses Apache POI and openpdf for PDF export
    implementation 'org.apache.poi:poi:5.4.0'
    implementation 'com.github.librepdf:openpdf:2.0.3'

    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'com.sun.mail:jakarta.mail:2.0.1'

    implementation 'xerces:xercesImpl:2.12.2'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.80'
    implementation 'com.google.api-client:google-api-client:2.6.0'

    implementation('commons-validator:commons-validator:1.9.0') {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }
    implementation 'com.github.ben-manes.caffeine:caffeine'

    implementation "org.apache.jclouds.api:filesystem:${jcloudsVersion}"
    implementation 'org.apache.commons:commons-compress:1.27.1'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"

    implementation 'com.networknt:json-schema-validator:1.5.6'

    implementation 'com.github.slugify:slugify:3.0.7'

    // add lombok support
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    //  Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework:spring-web'
    implementation 'org.springframework:spring-test'
    testImplementation 'org.flywaydb.flyway-test-extensions:flyway-spring-test:10.0.0'

    testImplementation 'io.jsonwebtoken:jjwt-impl:0.12.5'
    testImplementation 'io.jsonwebtoken:jjwt-jackson:0.12.5'
}

processResources {
    dependsOn(gitInfo)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/src/main/java"
        }
    }
}

tasks.register('updateApiSubmodule', Exec) {
    commandLine 'git', 'submodule', 'update', '--init', "--recursive"
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
}

tasks.register('downloadManifestSchema') {
    doLast {
        def schemaDir = file("${projectDir}/src/main/resources/schema")
        schemaDir.mkdirs()

        def schemaUrl = new URI(manifestSchemaUrl).toURL()
        def fileName = schemaUrl.path.tokenize('/').last()
        def schemaFile = new File(schemaDir, fileName)

        schemaFile.withOutputStream { out ->
            out << schemaUrl.openStream()
        }
    }
}

dependencyCheck {
    formats = [ReportGenerator.Format.HTML, ReportGenerator.Format.XML]
//    cveValidForHours = 1
}

bootJar {
    duplicatesStrategy = duplicatesStrategy.EXCLUDE
    project.hasProperty('gcp') ? getArchiveFileName().set('app.jar') : archiveClassifier.set('' +
            'exec')
}
jar.enabled(true)
jar.archiveClassifier.set('')

test {
    dependsOn(copyTestDatabaseScripts)
    useJUnitPlatform()
    maxParallelForks = 1
    testLogging {
        events = ['failed']
        exceptionFormat = 'short'
    }
    reports {
        junitXml.required = true
    }
}

compileJava.dependsOn tasks.named('openApiGenerate')
compileJava.dependsOn tasks.named('downloadManifestSchema')
publish.dependsOn build
publish.mustRunAfter build
checkCommitNeeded.dependsOn removeScripts

//replaced with drill4j
//build.dependsOn jacocoTestReport

drill {
    groupId = "report-portal"
    appId = "service-api"
    packagePrefixes = ["com/epam/ta/reportportal"]
    enableTestAgent {
        enabled = System.getenv("DRILL_AGENT_ENABLED") == "true" ?: false
        version = "0.23.5"
    }
    enableAppAgent {
        enabled = System.getenv("DRILL_AGENT_ENABLED") == "true" ?: false
        version = "0.9.7"
    }
}

springBoot {
    buildInfo {
        properties {
            name = "API Service"
            version = "${project.version}"
            additional = [
                    "description": "$project.description",
                    "branch"     : getCurrentGitBranch(),
                    "repo"       : "reportportal/service-api"
            ]
            excludes = ['time', 'artifact']
        }
    }
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/api-registry/api/openapi/reportportal-api.yaml"
    outputDir = "${layout.buildDirectory.get()}/generated"
    configFile = "$rootDir/src/main/resources/openapi/config.json"
    skipOverwrite = false
    cleanupOutput = true
}

tasks.preTagCommit.enabled = false
tasks.updateVersion.enabled = false
tasks.commitNewVersion.enabled = false
