/*
 * Copyright 2019 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'java'
    id "org.owasp.dependencycheck" version "5.3.1"
}

import org.owasp.dependencycheck.reporting.ReportGenerator

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/build-docker.gradle"
apply from: "$scriptsUrl/build-commons.gradle"
apply from: "$scriptsUrl/build-info.gradle"
apply from: "$scriptsUrl/release-service.gradle"
apply from: "$scriptsUrl/copy-database-scripts.gradle"
apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/jacoco.gradle"
project.hasProperty('sealightsSession') && sealightsSession?.trim() ? apply(from: 'sealights.gradle') : println('No sealights session')

repositories {
    mavenCentral()
    mavenLocal()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url 'https://jitpack.io' }
    maven { url "http://jasperreports.sourceforge.net/maven2" }
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts" }
}

//https://nvd.nist.gov/vuln/detail/CVE-2020-10683 (dom4j 2.1.3 version dependency) AND https://nvd.nist.gov/vuln/detail/CVE-2019-14900
ext['hibernate.version'] = '5.4.18.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-10693
ext['hibernate-validator.version'] = '6.1.5.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-13692
ext['postgresql.version'] = '42.2.13'
//https://nvd.nist.gov/vuln/detail/CVE-2020-9488
ext['log4j2.version'] = '2.13.2'

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:7ea4bf3')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-dao'
        compile 'com.epam.reportportal:commons-rules'
        compile 'com.epam.reportportal:commons-model'
        compile 'com.epam.reportportal:commons'
        compile 'com.epam.reportportal:commons-fonts'
        compile 'com.epam.reportportal:plugin-api'
    } else {
        compile 'com.github.reportportal:commons-dao:ebcd90ae'
        compile 'com.github.reportportal:commons-rules:331c402'
        compile 'com.github.reportportal:commons-model:2662a59b'
        compile 'com.github.reportportal:commons:7480d61'
        compile 'com.github.reportportal:commons-fonts:10d1054'
        compile 'com.github.reportportal:plugin-api:574b261'
    }

    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-quartz'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-amqp'
    compile('org.springframework.boot:spring-boot-starter-batch')

    //https://nvd.nist.gov/vuln/detail/CVE-2020-11996 AND https://nvd.nist.gov/vuln/detail/CVE-2020-9484
    //AND https://nvd.nist.gov/vuln/detail/CVE-2020-13934 AND https://nvd.nist.gov/vuln/detail/CVE-2020-13935
    //AND https://nvd.nist.gov/vuln/detail/CVE-2020-8022
    compile 'org.apache.tomcat.embed:tomcat-embed-core:9.0.37'
    compile 'org.apache.tomcat.embed:tomcat-embed-el:9.0.37'
    compile 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.37'
    //

    //https://nvd.nist.gov/vuln/detail/CVE-2020-5411
    compile('org.springframework.batch:spring-batch-core:4.2.3.RELEASE')
    compile('org.springframework.batch:spring-batch-infrastructure:4.2.3.RELEASE')


    // Optional for spring-boot-starter-amqp
    compile "com.rabbitmq:http-client:2.1.0.RELEASE"

    compile 'com.sun.mail:javax.mail:1.6.2'
    compile 'net.sf.jasperreports:jasperreports:6.12.2'
    compile 'com.lowagie:itext:2.1.7.js7'
    // JasperReport's export to XLS uses Apache POI
    compile 'org.apache.poi:poi:4.1.1'
    compile 'io.springfox:springfox-swagger2'

    ///// Security
    //https://nvd.nist.gov/vuln/detail/CVE-2020-5407 AND https://nvd.nist.gov/vuln/detail/CVE-2020-5408
    compile 'org.springframework.security:spring-security-core:5.2.4.RELEASE'
    compile 'org.springframework.security:spring-security-config:5.2.4.RELEASE'
    compile 'org.springframework.security:spring-security-web:5.2.4.RELEASE'
    //

    compile 'org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE'
    compile 'org.springframework.security:spring-security-jwt:1.0.11.RELEASE'
    compile 'org.springframework.security:spring-security-acl'
    compile 'com.github.ben-manes.caffeine:caffeine:2.8.0'

    //Logging
    //https://nvd.nist.gov/vuln/detail/CVE-2020-9488
    compile 'org.apache.logging.log4j:log4j-to-slf4j:2.13.2'

    // Metrics
    compile 'io.micrometer:micrometer-registry-prometheus:1.3.1'

    //  Tests
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.flywaydb.flyway-test-extensions:flyway-spring-test:6.1.0'
}

processResources {
    dependsOn(gitInfo)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

dependencyCheck {
    formats = [ReportGenerator.Format.HTML, ReportGenerator.Format.XML]
//    cveValidForHours = 1
}

bootJar {
    project.hasProperty('gcp') ? getArchiveFileName().set('app.jar') : archiveClassifier.set('exec')
}
jar.enabled(true)

test {
    dependsOn(copyTestDatabaseScripts)
    useJUnitPlatform()
    maxParallelForks = 1
    testLogging {
        events = ['failed']
        exceptionFormat = 'short'
    }
    reports {
        junitXml.enabled = true
    }
}

addDockerfileToGit.dependsOn createDockerfile
beforeReleaseBuild.dependsOn addDockerfileToGit
bintrayUpload.mustRunAfter build
afterReleaseBuild.dependsOn bintrayUpload
checkCommitNeeded.dependsOn removeScripts
build.dependsOn jacocoTestReport
