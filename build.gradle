/*
 * Copyright 2019 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'org.springframework.boot' version '2.5.12'
    id 'java'
    id "org.owasp.dependencycheck" version "5.3.1"
}

import org.owasp.dependencycheck.reporting.ReportGenerator

apply from: 'project-properties.gradle'
apply from: "$scriptsUrl/build-docker.gradle"
apply from: "$scriptsUrl/build-commons.gradle"
apply from: "$scriptsUrl/build-info.gradle"
apply from: "$scriptsUrl/release-service.gradle"
apply from: "$scriptsUrl/signing.gradle"
apply from: "$scriptsUrl/copy-database-scripts.gradle"
//apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/jacoco.gradle"
apply plugin: 'jacoco'
project.hasProperty('sealightsSession') && sealightsSession?.trim() ? apply(from: 'sealights.gradle') : println('No sealights session')

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    if (releaseMode) {
        dependencyRepos.forEach { path ->
            maven {
                setUrl("https://maven.pkg.github.com/reportportal/${path}")
                credentials {
                    username = findProperty("githubUserName")
                    password = findProperty("githubToken")
                }
            }
        }
    } else {
        maven { url 'https://jitpack.io' }
    }
//    maven { url "http://jasperreports.sourceforge.net/maven2" }
//    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts" }
    maven { url "https://jaspersoft.jfrog.io/artifactory/third-party-ce-artifacts" }
}

//https://nvd.nist.gov/vuln/detail/CVE-2020-10683 (dom4j 2.1.3 version dependency) AND https://nvd.nist.gov/vuln/detail/CVE-2019-14900
ext['hibernate.version'] = '5.4.18.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-10693
ext['hibernate-validator.version'] = '6.1.5.Final'
//https://nvd.nist.gov/vuln/detail/CVE-2020-13692
//ext['postgresql.version'] = '42.2.13'
//https://nvd.nist.gov/vuln/detail/CVE-2020-9488 and https://nvd.nist.gov/vuln/detail/CVE-2021-44228 and https://nvd.nist.gov/vuln/detail/CVE-2021-45046
ext['log4j2.version'] = '2.17.1'
ext['log4j-to-slf4j.version'] = '2.17.1'
//https://nvd.nist.gov/vuln/detail/cve-2022-22965
ext['spring-boot.version'] = '2.5.12'

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + '5.10.0' : 'com.github.reportportal:commons-bom:6aa55fc0')
        mavenBom('io.zonky.test.postgres:embedded-postgres-binaries-bom:12.9.0')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-dao'
        compile 'com.epam.reportportal:commons-rules'
        compile 'com.epam.reportportal:commons-model'
        compile 'com.epam.reportportal:commons'
        compile 'com.epam.reportportal:commons-fonts'
        compile 'com.epam.reportportal:plugin-api'
    } else {
        compile 'com.github.reportportal:commons-events:e337f8b7be'
        compile 'com.github.reportportal:commons-dao:6d24f713b1'
        compile 'com.github.reportportal:commons-rules:12ce5d4'
        compile 'com.github.reportportal:commons-model:e9c131f'
        compile 'com.github.reportportal:commons:ce2166b'
        compile 'com.github.reportportal:commons-fonts:d6e62dd'
        compile 'com.github.reportportal:plugin-api:fb8845f'
    }

    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-quartz'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-amqp'
    compile('org.springframework.boot:spring-boot-starter-batch')


    compile group: 'com.opencsv', name: 'opencsv', version: '5.7.1'

    // Fix CVE-2021-41079, CVE-2022-23181, CVE-2021-33037, CVE-2021-30640, CVE-2022-42252
    compile 'org.apache.tomcat.embed:tomcat-embed-core:9.0.68'
    compile 'org.apache.tomcat.embed:tomcat-embed-el:9.0.68'
    compile 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.68'
    //

    //https://nvd.nist.gov/vuln/detail/CVE-2020-5411
    compile('org.springframework.batch:spring-batch-core:4.2.3.RELEASE')
    compile('org.springframework.batch:spring-batch-infrastructure:4.2.3.RELEASE')


    // Optional for spring-boot-starter-amqp
    compile "com.rabbitmq:http-client:2.1.0.RELEASE"

    compile 'com.sun.mail:javax.mail:1.6.2'
    compile 'net.sf.jasperreports:jasperreports:6.12.2'
    compile 'com.lowagie:itext:2.1.7.js7'
    // JasperReport's export to XLS uses Apache POI
    compile 'org.apache.poi:poi:4.1.1'
    compile 'io.springfox:springfox-swagger2'

    ///// Security
    //https://nvd.nist.gov/vuln/detail/CVE-2020-5407 AND https://nvd.nist.gov/vuln/detail/CVE-2020-5408
    compile 'org.springframework.security:spring-security-core:5.5.8'
    compile 'org.springframework.security:spring-security-config:5.5.8'
    compile 'org.springframework.security:spring-security-web:5.5.8'
    //

    // Fix CVE-2022-22969
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE'
    compile 'org.springframework.security:spring-security-jwt:1.0.11.RELEASE'
    compile 'org.springframework.security:spring-security-acl'
    compile 'com.github.ben-manes.caffeine:caffeine:2.8.0'

    // Fix CVE-2022-22965, CVE-2022-22970
    compile 'org.springframework:spring-beans:5.3.20'
    // Fix CVE-2021-22060, CVE-2021-22096
    compile 'org.springframework:spring-core:5.3.20'
    // Fix CVE-2022-45685, CVE-2022-40150, CVE-2022-40149
    compile 'org.codehaus.jettison:jettison:1.5.2'
    // Fix CVE-2020-15522
    compile 'org.bouncycastle:bcprov-jdk15on:1.69'
    compile 'org.apache.commons:commons-compress:1.21'
    compile 'org.yaml:snakeyaml:1.32'
    compile 'org.hibernate:hibernate-core:5.4.24.Final'

    // Metrics
    compile 'io.micrometer:micrometer-registry-prometheus:1.7.10'

    //  Tests
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.flywaydb.flyway-test-extensions:flyway-spring-test:7.0.0'
}

processResources {
    dependsOn(gitInfo)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
    options.debug = true
    options.debugOptions.debugLevel = "source,lines,vars"
}

dependencyCheck {
    formats = [ReportGenerator.Format.HTML, ReportGenerator.Format.XML]
//    cveValidForHours = 1
}

bootJar {
    project.hasProperty('gcp') ? getArchiveFileName().set('app.jar') : archiveClassifier.set('exec')
}
jar.enabled(true)
jar.archiveClassifier.set('')

test {
    dependsOn(copyTestDatabaseScripts)
    useJUnitPlatform()
    maxParallelForks = 1
    testLogging {
        events = ['failed']
        exceptionFormat = 'short'
    }
    reports {
        junitXml.enabled = true
    }
}

jacocoTestReport {
    doFirst {
        classDirectories.setFrom(
                classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            'com/epam/ta/reportportal/core/events/AnalysisEvent.class',
                            'com/epam/ta/reportportal/auth/acl/ReportPortalAclAuthorizationStrategyImpl.class',
                            '**/Abstract*.class'
                    ])
                }
        )
    }

}

publish.dependsOn build
publish.mustRunAfter build
checkCommitNeeded.dependsOn removeScripts
build.dependsOn jacocoTestReport
